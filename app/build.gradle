buildscript {
    ext.kotlin_version = '1.0.6'
    repositories {
        maven { url 'https://maven.fabric.io/public' }
        mavenCentral()
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.21.7'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.gms:google-services:3.0.0'
    }
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
    mavenCentral()
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.czbix.v2ex"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 81
        versionName "1.24.6"

        resValue 'bool', 'isDebuggable', 'false'
    }
    packagingOptions {
        exclude '.readme'
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '.debug'
            resValue 'bool', 'isDebuggable', 'true'
            ext.enableCrashlytics = false

            multiDexEnabled true
        }
    }
    lintOptions {
        abortOnError false
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    productFlavors {
        prod {
        }
        dev {
            minSdkVersion 21
        }
    }
}

// Android support library version
ext.support_version = '25.1.0'
ext.gms_version = '10.0.1'

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:support-compat:$support_version"
    compile "com.android.support:support-core-utils:$support_version"
    compile "com.android.support:support-fragment:$support_version"
    compile 'com.google.guava:guava:19.0'
    compile 'com.squareup.okhttp3:okhttp:3.5.0'
    compile 'com.google.code.gson:gson:2.7'
    compile "com.android.support:recyclerview-v7:$support_version"
    compile "com.android.support:cardview-v7:$support_version"
    compile 'org.jsoup:jsoup:1.10.2'
    compile "com.android.support:design:$support_version"
    compile 'com.github.franmontiel:PersistentCookieJar:v1.0.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.0-beta4'

    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile ('com.github.bumptech.glide:okhttp3-integration:1.4.0') {
        exclude group: 'glide-parent'
    }

    compile "com.google.android.gms:play-services-analytics:$gms_version"
    compile 'com.google.firebase:firebase-messaging:10.0.1'
    compile 'com.firebase:firebase-jobdispatcher:0.5.2'
    compile('com.crashlytics.sdk.android:crashlytics:2.6.6@aar') {
        transitive = true;
    }
    compile "com.android.support:customtabs:$support_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    compile 'io.reactivex:rxandroid:1.2.1'
    // Because RxAndroid releases are few and far between, explicitly depend on RxJava's latest
    // version for bug fixes and new features.
    compile 'io.reactivex:rxjava:1.2.5'
    compile 'io.reactivex:rxkotlin:0.60.0'

    testCompile 'org.mockito:mockito-core:1.10.19'
    provided 'org.ccil.cowan.tagsoup:tagsoup:1.2.1'
}

apply plugin: 'com.google.gms.google-services'
