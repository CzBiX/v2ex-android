buildscript {
    ext {
        kotlin_version = '1.3.50'
        coroutinesVersion = '1.3.0-M1'
    }
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.31.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath 'com.google.gms:google-services:4.3.1'
    }
}

repositories {
    maven { url "https://jitpack.io" }
    maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-allopen'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.czbix.v2ex"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 106
        versionName "1.27.2"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        'room.schemaLocation': "$projectDir/schemas".toString(),
                        'room.incremental': 'true',
                        'room.expandProjection': 'true'
                ]
            }
        }
    }
    packagingOptions {
        exclude '.readme'
        exclude '/META-INF/LICENSE'
        exclude '/META-INF/README'
        exclude '/META-INF/CHANGES'
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '.debug'
            ext.enableCrashlytics = false
        }
    }
    lintOptions {
        abortOnError false
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        debug.java.srcDirs += 'src/debug/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    dataBinding {
        enabled = true
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true

            all {
                useJUnitPlatform()
            }
        }
    }
}

kapt {
    correctErrorTypes = true
}

allOpen {
    annotation("com.airbnb.epoxy.EpoxyModelClass")
}

androidExtensions {
    experimental = true
}

ext {
    gms_version = '17.0.0'
    glide_version = '4.9.0'
    epoxy_version = '3.7.0'
    lifecycle_version = '2.1.0'
    dragger_version = '2.24'
    paging_version = '2.1.0'
    room_version = '2.2.0-rc01'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.browser:browser:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.core:core-ktx:1.1.0'
    implementation 'androidx.fragment:fragment-ktx:1.1.0'
    implementation 'androidx.preference:preference-ktx:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'

    api 'com.google.guava:guava:28.0-android'
    implementation 'com.squareup.okhttp3:okhttp:4.1.0'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'org.jsoup:jsoup:1.12.1'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'com.github.franmontiel:PersistentCookieJar:v1.0.0'

    implementation ("com.github.bumptech.glide:glide:$glide_version") {
        exclude group: "com.android.support"
    }
    implementation "com.github.bumptech.glide:okhttp3-integration:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    implementation 'io.reactivex.rxjava2:rxjava:2.2.11'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-rx2:$coroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"

    implementation "com.google.android.gms:play-services-analytics:$gms_version"
    implementation "com.google.firebase:firebase-messaging:19.0.1"
    implementation 'com.firebase:firebase-jobdispatcher:0.5.2'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation 'com.jakewharton.timber:timber:4.7.1'

    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-reactivestreams-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"

    implementation "androidx.paging:paging-runtime-ktx:$paging_version"

    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    implementation "com.google.dagger:dagger:$dragger_version"
    implementation "com.google.dagger:dagger-android:$dragger_version"
    implementation "com.google.dagger:dagger-android-support:$dragger_version"
    kapt "com.google.dagger:dagger-compiler:$dragger_version"
    kapt "com.google.dagger:dagger-android-processor:$dragger_version"

    implementation "com.airbnb.android:epoxy:$epoxy_version"
    implementation "com.airbnb.android:epoxy-databinding:$epoxy_version"
    implementation "com.airbnb.android:epoxy-glide-preloading:$epoxy_version"
    implementation "com.airbnb.android:epoxy-paging:$epoxy_version"
    kapt "com.airbnb.android:epoxy-processor:$epoxy_version"

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.0-beta-2'
    debugImplementation 'com.facebook.stetho:stetho:1.5.1'
    debugImplementation 'com.facebook.stetho:stetho-okhttp3:1.5.1'

//    testImplementation 'androidx.test:core-ktx:1.2.0'
//    testImplementation 'org.mockito:mockito-core:1.10.19'
    testImplementation 'io.kotlintest:kotlintest-runner-junit5:3.4.0'

    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'io.kotlintest:kotlintest-assertions:3.4.0'

    compileOnly 'org.ccil.cowan.tagsoup:tagsoup:1.2.1'
}

apply plugin: 'com.google.gms.google-services'
