buildscript {
    ext {
        kotlin_version = '1.3.41'
        coroutinesVersion = '1.3.0-M1'
    }
    repositories {
        maven { url 'https://maven.fabric.io/public' }
        jcenter()
        google()
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.26.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath 'com.google.gms:google-services:4.3.0'
    }
}

repositories {
    maven { url "https://jitpack.io" }
    maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-allopen'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.czbix.v2ex"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 97
        versionName "1.25.6"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    packagingOptions {
        exclude '.readme'
        exclude '/META-INF/LICENSE'
        exclude '/META-INF/README'
        exclude '/META-INF/CHANGES'
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '.debug'
            ext.enableCrashlytics = false
        }
    }
    lintOptions {
        abortOnError false
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    dataBinding {
        enabled = true
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            all {
                useJUnitPlatform()
            }
        }
    }
}

kapt {
    correctErrorTypes = true
}

allOpen {
    annotation("com.airbnb.epoxy.EpoxyModelClass")
}


// Android support library version
ext.androidx_version = '1.0.0'
ext.gms_version = '17.0.0'
ext.glide_version = '4.9.0'
ext.epoxy_version = '3.7.0'

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.core:core-ktx:1.0.2'
    implementation "androidx.legacy:legacy-support-core-utils:$androidx_version"
    implementation 'androidx.fragment:fragment-ktx:1.0.0'
    api 'com.google.guava:guava:28.0-android'
    implementation 'com.squareup.okhttp3:okhttp:4.1.0'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation "androidx.recyclerview:recyclerview:$androidx_version"
    implementation "androidx.cardview:cardview:$androidx_version"
    implementation 'org.jsoup:jsoup:1.12.1'
    implementation "com.google.android.material:material:$androidx_version"
    implementation 'com.github.franmontiel:PersistentCookieJar:v1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    implementation ("com.github.bumptech.glide:glide:$glide_version") {
        exclude group: "com.android.support"
    }
    implementation "com.github.bumptech.glide:okhttp3-integration:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    implementation 'io.reactivex.rxjava2:rxjava:2.2.11'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-rx2:$coroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"

    implementation "com.google.android.gms:play-services-analytics:$gms_version"
    implementation "com.google.firebase:firebase-messaging:19.0.1"
    implementation 'com.firebase:firebase-jobdispatcher:0.5.2'
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.9@aar') {
        transitive = true
    }
    implementation "androidx.browser:browser:$androidx_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"


    implementation "com.airbnb.android:epoxy:$epoxy_version"
    implementation "com.airbnb.android:epoxy-databinding:$epoxy_version"
    kapt "com.airbnb.android:epoxy-processor:$epoxy_version"

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.0-beta-2'

    testImplementation 'org.mockito:mockito-core:1.10.19'

    androidTestImplementation 'androidx.test:core-ktx:1.2.0'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'com.google.truth:truth:1.0'

    compileOnly 'org.ccil.cowan.tagsoup:tagsoup:1.2.1'
}

apply plugin: 'com.google.gms.google-services'
