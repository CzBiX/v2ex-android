buildscript {
    ext {
        kotlin_version = '1.9.23'
        coroutinesVersion = '1.3.3'
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath 'com.google.gms:google-services:4.4.1'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.9.9'
    }
}

repositories {
    maven { url "https://jitpack.io" }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-allopen'
apply plugin: 'kotlin-parcelize'

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdk 34
    namespace 'com.czbix.v2ex'

    defaultConfig {
        applicationId "com.czbix.v2ex"
        minSdk 21
        targetSdk 33
        versionCode 118
        versionName "1.27.15"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        'room.schemaLocation': "$projectDir/schemas".toString(),
                        'room.incremental': 'true',
                        'room.expandProjection': 'true'
                ]
            }
        }
    }
    packagingOptions {
        resources {
            excludes += ['.readme', '/META-INF/LICENSE', '/META-INF/README', '/META-INF/CHANGES']
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '.debug'
        }
    }
    lint {
        abortOnError false
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        debug.java.srcDirs += 'src/debug/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildFeatures {
        dataBinding true
        viewBinding true
        buildConfig true
    }

    testOptions {
        unitTests {
            includeAndroidResources true
            returnDefaultValues true

            all {
                useJUnitPlatform()
            }
        }
    }
}

kapt {
    correctErrorTypes = true
}

allOpen {
    annotation("com.airbnb.epoxy.EpoxyModelClass")
}

ext {
    glide_version = '4.14.2'
    epoxy_version = '4.6.3'
    lifecycle_version = '2.2.0'
    dragger_version = '2.51'
    paging_version = '2.1.1'
    room_version = '2.6.1'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.browser:browser:1.2.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.fragment:fragment-ktx:1.2.4'
    implementation 'androidx.preference:preference-ktx:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.0.0'
    implementation 'androidx.drawerlayout:drawerlayout:1.1.0-beta01'

    api 'com.google.guava:guava:28.0-android'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'org.jsoup:jsoup:1.12.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'com.github.franmontiel:PersistentCookieJar:v1.0.0'

    implementation ("com.github.bumptech.glide:glide:$glide_version") {
        exclude group: "com.android.support"
    }
    implementation "com.github.bumptech.glide:okhttp3-integration:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    implementation 'io.reactivex.rxjava2:rxjava:2.2.11'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-rx2:$coroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"

    implementation 'com.google.firebase:firebase-analytics:21.5.1'
    implementation 'com.google.firebase:firebase-crashlytics:18.6.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation 'com.jakewharton.timber:timber:4.7.1'

    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-reactivestreams-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"

    implementation "androidx.paging:paging-runtime-ktx:$paging_version"

    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    implementation "com.google.dagger:dagger:$dragger_version"
    implementation "com.google.dagger:dagger-android:$dragger_version"
    implementation "com.google.dagger:dagger-android-support:$dragger_version"
    kapt "com.google.dagger:dagger-compiler:$dragger_version"
    kapt "com.google.dagger:dagger-android-processor:$dragger_version"

    implementation "com.airbnb.android:epoxy:$epoxy_version"
    implementation "com.airbnb.android:epoxy-databinding:$epoxy_version"
    implementation "com.airbnb.android:epoxy-glide-preloading:$epoxy_version"
    implementation "com.airbnb.android:epoxy-paging:$epoxy_version"
    kapt "com.airbnb.android:epoxy-processor:$epoxy_version"

    debugImplementation 'com.facebook.stetho:stetho:1.5.1'
    debugImplementation 'com.facebook.stetho:stetho-okhttp3:1.5.1'

//    testImplementation 'androidx.test:core-ktx:1.2.0'
//    testImplementation 'org.mockito:mockito-core:1.10.19'
    testImplementation 'io.kotlintest:kotlintest-runner-junit5:3.4.0'

    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'io.kotlintest:kotlintest-assertions:3.4.0'

    compileOnly 'org.ccil.cowan.tagsoup:tagsoup:1.2.1'
}
